import java.nio.file.Files
import java.nio.file.Paths

// TODO: Convert to kotlin-based build.gradle as soon as possible
buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}


intellij {
    version '15.0.1'
//    plugins 'coverage'
    pluginName 'intellij_swagger_codegen'

//    version ideaVersion
//    downloadSources Boolean.valueOf(downloadIdeaSources)
//
    updateSinceUntilBuild false
    sandboxDirectory project.rootDir.canonicalPath + "/.sandbox"

    publish {
        pluginId 'intellij_swagger_codegen'
        if(Files.exists(Paths.get("${System.getProperty("user.home")}/.gradle/config/intellij.properties"))) {
            apply from: "${System.getProperty("user.home")}/.gradle/config/intellij.properties"
        }
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // [temporary] swagger-parser is using an older version of these deps. Newer version breaks compile.
    compile 'org.slf4j:slf4j-api:1.6.3'
    compile 'org.slf4j:slf4j-ext:1.6.3'
    compile 'org.slf4j:slf4j-simple:1.6.3'
    compile('io.swagger:swagger-codegen:2.2.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-ext'
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    runtime group: 'io.swagger', name: 'swagger-codegen', version: '2.2.1', transitive: true
}

idea {
    project {
        jdkName         = javaVersion
        languageLevel   = javaVersion
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

version = "${version}.$buildNumber"
